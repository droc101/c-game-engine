macro(enable_options)
    foreach (OPTION_TO_SET IN ITEMS ${ARGN})
        set(${OPTION_TO_SET} ON)
    endforeach ()
endmacro()

macro(disable_options)
    foreach (OPTION_TO_SET IN ITEMS ${ARGN})
        set(${OPTION_TO_SET} OFF)
    endforeach ()
endmacro()

function(detect_platform)
    include(CheckCSourceCompiles)
    set(CMAKE_TRY_COMPILE_TARGET_TYPE "STATIC_LIBRARY")
    check_c_source_compiles("
    #if !((defined(__amd64__) || defined(__amd64) || defined(__x86_64__) || defined(__x86_64) || defined(_M_X64) || defined(_M_AMD64)) && !defined(_M_ARM64EC))
        #error
    #endif" x86_64)
    if (NOT x86_64)
        check_c_source_compiles("
        #if !(defined(__aarch64__) || defined(_M_ARM64))
            #error
        #endif" arm64)
        if (arm64)
            message(STATUS "Detected ARM64 architecture (no support will be provided)")
        else ()
            message(FATAL_ERROR "Unsupported architecture: ${CMAKE_SYSTEM_PROCESSOR}. Supported architectures are x86_64 and aarch64.")
        endif ()
    endif ()
    if (NOT CMAKE_SIZEOF_VOID_P EQUAL 8)
        message(FATAL_ERROR "GAME only supports 64-bit systems (CMAKE_SIZEOF_VOID_P=${CMAKE_SIZEOF_VOID_P})")
    endif ()
    if (NOT CMAKE_C_BYTE_ORDER STREQUAL "LITTLE_ENDIAN")
        message(FATAL_ERROR "GAME only supports little endian systems")
    endif ()
endfunction()

function(enable_lto)
    include(CheckIPOSupported)
    check_ipo_supported(RESULT result)
    if (result)
        set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE PARENT_SCOPE)
        message(STATUS "IPO/LTO is supported")
    else ()
        message(WARNING "IPO/LTO is not supported")
    endif ()
endfunction()
